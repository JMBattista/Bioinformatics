allprojects {
    apply plugin: 'maven'

    group = 'com.vitreoussoftware.bioinformatics'
    version = '0.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    repositories {
        mavenLocal()

        maven { url "http://repo.maven.apache.org/maven2" }
    }

    /**
      * Defining scalaTest task here to share with sub projects
      */
    task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
        main = 'org.scalatest.tools.Runner'
        args = ['-R', 'build/classes/test', '-o']
        classpath = sourceSets.test.runtimeClasspath
    }

    test.dependsOn scalaTest // ensure that the scalaTest task is run when running tests


    dependencies {
        compile 'com.google.guava:guava:19.0'

        // Lombok dependencies, must be both compile and testcompile
        compileOnly "org.projectlombok:lombok:1.16.10"
        testCompileOnly "org.projectlombok:lombok:1.16.10"

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
        testCompile group: 'org.scalatest', name: 'scalatest_2.11', version: '2.2.1'
        testCompile group: 'org.scalacheck', name: 'scalacheck_2.11', version: '1.11.5'
    }
}
