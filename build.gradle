buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    }
}

plugins {
    id 'jacoco'
}
 

allprojects {
    apply plugin: "jacoco" // code coverage

    group = 'com.vitreoussoftware.bioinformatics'
    version = '0.1.0-SNAPSHOT'

    repositories {
        mavenLocal()

        maven { url "http://repo.maven.apache.org/maven2" }
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()

        maven { url "http://repo.maven.apache.org/maven2" }
    }

    //'pitest' for plugin versions <1.1.0
    apply plugin: 'info.solidsoft.pitest'
    pitest {
        threads = 4

        if (project.name in ['ScalaTestSpec']) {
            failWhenNoMutations = false
        }
    }
    check.dependsOn "pitest"

    /**
      * Defining scalaTest task here to share with sub projects
      */
    task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
        main = 'org.scalatest.tools.Runner'
        args = ['-R', 'build/classes/test', '-o']
        classpath = sourceSets.test.runtimeClasspath
    }

    /**
     * https://docs.gradle.org/current/userguide/jacoco_plugin.html
     * Configure code coverage
     */
    jacoco {
        toolVersion = "0.7.6.201602180812"
        reportsDir = file("$buildDir/jacoco")
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
            html.destination "${buildDir}/jacocoHtml"
        }
    }

    test {
        jacoco {
            append = false
            destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
            classDumpFile = file("$buildDir/jacoco/classpathdumps")
        }
    }

    test.dependsOn scalaTest // ensure that the scalaTest task is run when running tests


    dependencies {
        compile 'com.google.guava:guava:19.0'

        // Lombok dependencies, must be both compile and testcompile
        compileOnly "org.projectlombok:lombok:1.16.10"
        testCompileOnly "org.projectlombok:lombok:1.16.10"

        compile 'com.google.guava:guava:19.0'

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
        testCompile group: 'org.scalatest', name: 'scalatest_2.11', version: '2.2.1'
        testCompile group: 'org.scalacheck', name: 'scalacheck_2.11', version: '1.11.5'
    }
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}
